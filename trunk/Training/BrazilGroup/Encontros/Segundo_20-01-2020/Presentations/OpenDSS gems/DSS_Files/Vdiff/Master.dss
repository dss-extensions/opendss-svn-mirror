// -----------------------------------------------------------------------//
// Autor: Paulo Radatz
// e-mail: pradatz@epri.com
// -----------------------------------------------------------------------//

// Reinicia o Programa
Clear

// Dados de Fronteira
New Circuit.Equivalente bus1=A pu=1.02 basekv=138 
~ Z0=[0.025862916, 0.077588748] Z1=[0.023094242, 0.092376969]

// Dados do Transformador
New Transformer.Trafo phases=3 windings=2 %loadloss=0.15 xhl=5 %noloadloss=0.015 %imag=2 
~ wdg=1 bus=A kv=138 kva=3000 conn=delta
~ wdg=2 bus=B kv=13.8 kva=3000 conn=wye
~ sub=Yes subname=Subestacao

// Dados dos Trechos
New Line.LinhaBC bus1=B.1.2.3.0 bus2=C.1.2.3.4 length=0.8 units=km 
New Line.LinhaCD bus1=C.1.2.3 bus2=D.1.2.3 length=0.6 units=km

// Dados das Cargas
// Model=1 -> Potencia Constante
New Load.CargaC phases=1 bus1=C.1 kv=7.9674 kw=500 pf=0.92 model=1
New Load.CargaD phases=3 bus1=D conn=wye kv=13.8 kw=2000 pf=0.92 model=1

// Medidor
New EnergyMeter.MedidorSub element=Transformer.Trafo terminal=1

// Definindo Tensoes de base
Set voltagebases=[138 13.8]
Calcvoltagebases
  
// SnapShot Mode
Set mode=SnapShot

Solve
Save voltages

set loadmult=0.5
solve
Vdiff





